import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.20'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "16"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "16"
    }

    sourceCompatibility = "16"

    project.ext.jomlVersion = "1.10.1"
    project.ext.lwjglVersion = "3.3.1-SNAPSHOT"
    switch (OperatingSystem.current()) {
        case OperatingSystem.LINUX:
            def osArch = System.getProperty("os.arch")
            project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                    ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                    : "natives-linux"
            break
        case OperatingSystem.WINDOWS:
            def osArch = System.getProperty("os.arch")
            project.ext.lwjglNatives = osArch.contains("64")
                    ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                    : "natives-windows-x86"
            break
    }

    dependencies {
        compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
        compileOnly 'org.slf4j:slf4j-api:1.7.32'

        compileOnly platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
        compileOnly "org.lwjgl:lwjgl"
        compileOnly "org.lwjgl:lwjgl-vulkan"
        compileOnly "org.joml:joml:${jomlVersion}"
    }
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    runtimeOnly 'ch.qos.logback:logback-core:1.2.3'

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-openvr"
    implementation "org.lwjgl:lwjgl-openxr"
    implementation "org.lwjgl:lwjgl-vulkan"
    implementation "org.joml:joml:${jomlVersion}"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openvr::$lwjglNatives"

    testImplementation platform('org.junit:junit-bom:5.7.2')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

test {
    useJUnitPlatform()
}

project(':standard-plugin') {
    dependencies {
        implementation project(':')
        testImplementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
        testImplementation "org.lwjgl:lwjgl"
        testImplementation platform('org.junit:junit-bom:5.7.2')
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    }
}

project(':debug-plugin') {
    dependencies {
        implementation project(':')
    }
}

shadowJar {
    manifest {
        attributes ('Main-Class': 'dragons.init.InitKt')
    }
}
